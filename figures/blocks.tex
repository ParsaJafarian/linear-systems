\usepath[/home/adityam/Projects/presentations]

\enablemode[euler]
\environment env-fonts.tex

\startMPdefinitions
% Arrowhead Modifications for TAOCP. Copied from some webpage of Knuth. 
% These arrows are better in appearance than the default mp arrows.
ahangle := 65;
ahlength := 10 ;
vardef arrowhead expr p =
  save q, e, f, g; path q; pair e; pair f; pair g;
  e = point length p of p;
  q = gobble(p shifted -e cutafter makepath(pencircle scaled 2ahlength))
    cuttings;
  f = point 0 of (q rotated 0.5ahangle) shifted e;
  g = point length q of (reverse q rotated -0.5ahangle) shifted e;
  f .. {dir (angle direction length q of (q rotated 0.5ahangle) - 0.3ahangle)}e
    & e{dir (angle direction 0 of ((reverse q) rotated -0.5ahangle)+0.3ahangle)} .. g
enddef;

vardef arrowpath expr p =
  p
enddef;

def mfun_draw_arrow_path text t =
    if autoarrows :
        set_ahlength(t) ;
    fi
    draw arrowpath mfun_arrow_path t ;
    draw arrowhead mfun_arrow_path t ;
    endgroup ;
enddef ;

def mfun_draw_arrow_path_double text t =
    if autoarrows :
        set_ahlength(t) ;
    fi
    draw arrowpath (reverse arrowpath mfun_arrow_path) t ;
    draw arrowhead mfun_arrow_path t ;
    draw arrowhead reverse mfun_arrow_path t ;
    endgroup ;
enddef ;


  primarydef a -+ b =
    a -- (xpart b, ypart a) -- b
  enddef ;

  primarydef a +- b =
    a -- (xpart a, ypart b) -- b
  enddef ;

  input boxes;
  input mp-sketch;
  sketchypaths;
  sketch_amount := 1.3bp;
  pickup pencircle scaled 2bp;
  defaultdx := 10bp;
  defaultdy := 10bp;

\stopMPdefinitions


\starttext

\startstackingsteps[{0,1},{0,1,-1},{0,2,-2}]

\startMPpage[offset=1dk, stacking={\namedstackingparameter\empty{criterium}}] % {{{ Example 1 begingroup;
  sketchypaths;

  ux := 1cm;
  uy := 1cm;

  pair split_one;
  boxit.G_one("$G_1(s)$");
  boxit.G_two("$G_2(s)$");
  boxit.G_three("$G_3(s)$");
  boxit.H_one("$H_1(s)$");
  boxit.H_two("$H_2(s)$");
  circleit.sum_one("\tfc $+$");

  sum_one.w - G_one.e = (1.5ux,0);
  G_two.w - sum_one.e = (1.5ux,0);
  G_three.w - G_two.e = (1.5ux,0);
  G_three.s - H_one.n = (0, uy);
  G_two.s   - H_two.n = (0, uy);


  
  boxes_fixsize(sum_one, G_one, G_two, G_three, H_one, H_two);
  boxes_fixpos(sum_one, G_one, G_two, G_three, H_one, H_two);

  draw boxes_pic sum_one shifted sum_one.c;
  draw boxes_pic G_one shifted G_one.c;
  draw boxes_pic G_two shifted G_two.c withstacking 1;
  draw boxes_pic G_three shifted G_three.c withstacking 1;

  draw boxes_pic H_one shifted H_one.c withstacking 1;
  draw boxes_pic H_two shifted H_two.c withstacking 1;

  boxit.G_simplified("$G_2(s)G_3(s)$");
  boxit.H_simplified("$H_1(s)H_2(s)$");

  G_simplified.w = G_two.w;
  G_simplified.e = G_three.e;
  G_simplified.n = 0.5[G_two.n, G_three.n];
  G_simplified.s = 0.5[G_two.s, G_three.s];

  H_simplified.w = H_two.w;
  H_simplified.e = H_one.e;
  H_simplified.n = 0.5[H_one.n, H_two.n];
  H_simplified.s = 0.5[H_one.s, H_two.s];

  boxes_fixsize(G_simplified, H_simplified);
  boxes_fixpos(G_simplified, H_simplified);

  draw boxes_pic G_simplified shifted G_simplified.c withstacking 2;
  draw boxes_pic H_simplified shifted H_simplified.c withstacking 2;

  pair Input, Output;

  Input  := G_one.w - (1.5ux,0);
  split_one := G_three.e + (ux,0);
  Output := split_one + (ux,0);

  for i = 1 upto 5 :
    sketch_amount := 1.3bp;
    draw bpath G_one cornered 10bp;
    draw bpath G_two cornered 10bp withstacking 1;
    draw bpath G_three cornered 10bp withstacking 1;
    draw bpath H_one cornered 10bp withstacking 1;
    draw bpath H_two cornered 10bp withstacking 1;
    draw bpath sum_one;
    
    draw bpath G_simplified cornered 10bp withstacking 2;
    draw bpath H_simplified cornered 10bp withstacking 2;

    sketch_amount := 0.6bp;
    drawarrow Input -- lft G_one.w;
    drawarrow G_one.e -- lft sum_one.w;
    drawarrow sum_one.e -- lft G_two.w;
    drawarrow G_two.e -- lft G_three.w withstacking 1;
    drawarrow G_three.e -- Output;
    drawarrow H_one.w -- rt H_two.e withstacking 1 ;
    drawarrow H_two.w -+ bot sum_one.s ;
    drawarrow split_one +- lft H_one.e;
  endfor;

  newpath back_one, back_two, back_three;

  back_one = bbox image(drawboxed(G_two,G_three)) enlarged 5pt;
  back_two = bbox image(drawboxed(H_one,H_two)) enlarged 5pt;

  fill back_one withcolor 0.85white withstacking -1;
  draw back_one dashed evenly withstacking -1;

  fill back_two withcolor 0.85white withstacking -1;
  draw back_two dashed evenly withstacking -1;
  
  back_three = bbox image(
        drawboxed(sum_one, G_simplified, H_simplified);
        draw fullcircle shifted split_one;
      ) enlarged 5pt;

  fill back_three withcolor 0.85white withstacking -2;
  draw back_three dashed evenly withstacking -2;



  label.ulft("\tfx $+$", sum_one.w + (2bp,2bp)); 
  label.lrt("\tfx $-$", sum_one.s + (2bp,0) ); 
  label.lft("$U(s)$", Input);
  label.rt("$Y(s)$", Output);

  currentpicture := currentpicture scaled 1.5;

  endgroup;
\stopMPpage % }}}
\stopstackingsteps

\startstackingsteps[{0,1,13},{0,1,-1,13},{0,2,-2,13},{0,13,2,-3},{0,4,-4}]

\startMPpage[offset=1dk, stacking={\namedstackingparameter\empty{criterium}}] % {{{ Example 2 begingroup;
  sketchypaths;

  ux := 1cm;
  uy := 1cm;

  pair split_one;
  boxit.G_one("$G_1(s)$");
  boxit.G_two("$G_2(s)$");
  boxit.G_three("$G_3(s)$");
  boxit.H_one("$H_1(s)$");
  boxit.H_two("$H_2(s)$");
  boxit.H_three("$H_3(s)$");
  circleit.sum_one("\tfc $+$");
  circleit.sum_two("\tfc $+$");
  circleit.sum_three("\tfc $+$");

  sum_one.w - G_one.e = (1.5ux,0);
  sum_two.w - sum_one.e = (1ux,0);
  sum_three.w - sum_two.e = (1ux,0);
  G_two.w - sum_three.e = (1.5ux,0);
  G_three.w - G_two.e = (1.5ux,0);

  0.5[G_two.s, G_three.s]  - H_one.n = (0, uy);
  H_one.s - H_two.n = (0,uy);
  H_two.s - H_three.n = (0,uy);

  boxes_fixsize(sum_one, sum_two, sum_three, G_one, G_two, G_three, H_one, H_two, H_three);
  boxes_fixpos(sum_one, sum_two, sum_three, G_one, G_two, G_three, H_one, H_two, H_three);

  draw boxes_pic sum_one shifted sum_one.c;
  draw boxes_pic sum_two shifted sum_two.c withstacking 13;
  draw boxes_pic sum_three shifted sum_three.c withstacking 1;
  draw boxes_pic G_one shifted G_one.c;
  draw boxes_pic G_two shifted G_two.c withstacking 1;
  draw boxes_pic G_three shifted G_three.c withstacking 1;

  draw boxes_pic H_one shifted H_one.c withstacking 1;
  draw boxes_pic H_two shifted H_two.c withstacking 13;
  draw boxes_pic H_three shifted H_three.c;

  boxit.feedback_one("$\dfrac{G_2(s) G_3(s)}{1 + G_2(s) G_3(s) H_1(s)}$");

  ypart feedback_one.n = ypart G_two.n ;
  ypart feedback_one.s = ypart H_one.s ;
  xpart feedback_one.e = xpart G_three.e;
  xpart feedback_one.w = xpart sum_three.w;

  boxes_fixsize(feedback_one);
  boxes_fixpos(feedback_one);

  draw boxes_pic feedback_one shifted feedback_one.c withstacking 2;

  boxit.feedback_two("$\dfrac{ G_2(s) G_3(s) } { 1 + G_2(s)G_3(s) H_1(s) + G_2(s)G_3(s)H_2(s) }$");

  ypart feedback_two.n = ypart G_two.n ;
  ypart feedback_two.s = ypart H_two.s ;
  xpart feedback_two.e = xpart G_three.e;
  xpart feedback_two.w = xpart sum_two.w;

  boxes_fixsize(feedback_two);
  boxes_fixpos(feedback_two);

  draw boxes_pic feedback_two shifted feedback_two.c withstacking 4;

  pair Input, Output;

  Input  := G_one.w - (1.5ux,0);
  split_one := G_three.e + (ux,0);
  Output := split_one + (ux,0);

  for i = 1 upto 5 :
    sketch_amount := 1.3bp;
    draw bpath G_one cornered 10bp;
    draw bpath G_two cornered 10bp withstacking 1;
    draw bpath G_three cornered 10bp withstacking 1;
    draw bpath H_one cornered 10bp withstacking 1;
    draw bpath H_two cornered 10bp withstacking 13;
    draw bpath H_three cornered 10bp ;
    draw bpath sum_one;
    draw bpath sum_two withstacking 13;
    draw bpath sum_three withstacking 1;

    draw bpath feedback_one cornered 10bp withstacking 2;
    draw bpath feedback_two cornered 10bp withstacking 4;
    
    sketch_amount := 0.6bp;
    drawarrow Input -- lft G_one.w;
    drawarrow G_one.e -- lft sum_one.w;
    drawarrow sum_one.e -- lft sum_two.w;
    drawarrow sum_two.e -- lft sum_three.w withstacking 13;
    drawarrow sum_three.e -- lft G_two.w withstacking 1;
    drawarrow G_two.e -- lft G_three.w withstacking 1;
    drawarrow G_three.e -- Output;

    drawarrow split_one +- lft H_one.e withstacking 1;
    drawarrow H_one.w -+ bot sum_three.s withstacking 1;

    drawarrow split_one +- lft H_two.e withstacking 13;
    drawarrow H_two.w -+ bot sum_two.s withstacking 13;

    drawarrow split_one +- lft H_three.e;
    drawarrow H_three.w -+ bot sum_one.s ;
  endfor;

  newpath back_one, back_two;

  back_one = bbox image(
        drawboxed(sum_three, G_three, H_one); 
        draw fullcircle shifted split_one;
      ) enlarged 5pt;

  fill back_one withcolor 0.85white withstacking -1;
  draw back_one dashed evenly withstacking -1;

  back_two = bbox image(
        drawboxed(sum_two, feedback_one, H_two); 
        draw fullcircle shifted split_one;
      ) enlarged 5pt;

  fill back_two withcolor 0.85white withstacking -3;
  draw back_two dashed evenly withstacking -3;


  label.ulft("\tfx $+$", sum_one.w + (2bp,2bp)); 
  label.lrt("\tfx $-$",  sum_one.s + (2bp,0) ); 

  label.ulft("\tfx $+$", sum_two.w + (2bp,2bp)) withstacking 13; 
  label.lrt("\tfx $-$",  sum_two.s + (2bp,0) )  withstacking 13; 

  label.ulft("\tfx $+$", sum_three.w + (2bp,2bp)) withstacking 1; 
  label.lrt("\tfx $-$",  sum_three.s + (2bp,0) )  withstacking 1; 

  label.lft("$U(s)$", Input);
  label.rt("$Y(s)$", Output);

  currentpicture := currentpicture scaled 1.5;

  endgroup;
\stopMPpage % }}}
\stopstackingsteps

\startstackingsteps[{-1,0,1,13},{-2,0,2,13}]

\startMPpage[offset=1dk, stacking={\namedstackingparameter\empty{criterium}}] % {{{ Example 2 begingroup;
  sketchypaths;

  ux := 1cm;
  uy := 1cm;

  pair split_one;
  boxit.G_one("$G_1(s)$");
  boxit.G_two("$G_2(s)$");
  boxit.G_three("$G_3(s)$");
  boxit.H_one("$H_1(s)$");
  boxit.H_two("$H_2(s)$");
  boxit.H_three("$H_3(s)$");
  circleit.sum_one("\tfc $+$");
  circleit.sum_two("\tfc $+$");
  circleit.sum_three("\tfc $+$");

  sum_one.w - G_one.e = (1.5ux,0);
  sum_two.w - sum_one.e = (1ux,0);
  sum_three.w - sum_two.e = (1ux,0);
  G_two.w - sum_three.e = (1.5ux,0);
  G_three.w - G_two.e = (1.5ux,0);

  0.5[G_two.s, G_three.s]  - H_one.n = (0, uy);
  H_one.s - H_two.n = (0,uy);
  H_two.s - H_three.n = (0,uy);

  boxes_fixsize(sum_one, sum_two, sum_three, G_one, G_two, G_three, H_one, H_two, H_three);
  boxes_fixpos(sum_one, sum_two, sum_three, G_one, G_two, G_three, H_one, H_two, H_three);

  draw boxes_pic sum_one shifted sum_one.c;
  draw boxes_pic sum_two shifted sum_two.c withstacking 1;
  draw boxes_pic sum_three shifted sum_three.c withstacking 1;
  draw boxes_pic G_one shifted G_one.c;
  draw boxes_pic G_two shifted G_two.c;
  draw boxes_pic G_three shifted G_three.c;

  draw boxes_pic H_one shifted H_one.c withstacking 1;
  draw boxes_pic H_two shifted H_two.c withstacking 1;
  draw boxes_pic H_three shifted H_three.c withstacking 1;

  boxit.feedback_one("$H_1(s) + H_2(s) + H_3(s)$");

  ypart feedback_one.n = ypart H_one.n ;
  ypart feedback_one.s = ypart H_three.s ;
  xpart feedback_one.e = xpart H_two.e;

  boxes_fixsize(feedback_one);
  boxes_fixpos(feedback_one);

  draw boxes_pic feedback_one shifted feedback_one.c withstacking 2;

  pair Input, Output;

  Input  := G_one.w - (1.5ux,0);
  split_one := G_three.e + (ux,0);
  Output := split_one + (ux,0);

  for i = 1 upto 5 :
    sketch_amount := 1.3bp;
    draw bpath G_one cornered 10bp;
    draw bpath G_two cornered 10bp;
    draw bpath G_three cornered 10bp;
    draw bpath H_one cornered 10bp withstacking 1;
    draw bpath H_two cornered 10bp withstacking 1;
    draw bpath H_three cornered 10bp withstacking 1;
    draw bpath sum_one;
    draw bpath sum_two withstacking 1;
    draw bpath sum_three withstacking 1;

    draw bpath feedback_one cornered 10bp withstacking 2;
    
    sketch_amount := 0.6bp;
    drawarrow Input -- lft G_one.w;
    drawarrow G_one.e -- lft sum_one.w;
    drawarrow sum_one.e -- lft sum_two.w withstacking 1;
    drawarrow sum_two.e -- lft sum_three.w withstacking 1;
    drawarrow sum_three.e -- lft G_two.w withstacking 1;

    drawarrow sum_one.e -- lft G_two.w withstacking 2;

    drawarrow G_two.e -- lft G_three.w;
    drawarrow G_three.e -- Output;

    drawarrow split_one +- lft H_one.e withstacking 1;
    drawarrow H_one.w -+ bot sum_three.s withstacking 1;

    drawarrow split_one +- lft H_two.e;
    drawarrow H_two.w -+ bot sum_two.s withstacking 1;
    drawarrow feedback_one.w -+ bot sum_one.s withstacking 2;

    drawarrow split_one +- lft H_three.e withstacking 1;
    drawarrow H_three.w -+ bot sum_one.s withstacking 1;
  endfor;

  newpath back_one, back_two, back_three;

  back_one = bbox image(
        drawboxed(sum_one,sum_two,sum_three); 
      ) enlarged 5pt;
  
  back_two = bbox image(
        drawboxed(H_one,H_two,H_three); 
      ) enlarged 5pt;

  back_three = ulcorner back_one -- urcorner back_one
            -- (xpart lrcorner back_one, ypart ulcorner back_two)
            -- (xpart split_one + 5pt, ypart urcorner back_two)
            -- (xpart split_one + 5pt, ypart lrcorner back_two)
            -- (xpart llcorner back_one, ypart llcorner back_two)
            -- cycle ;

  fill back_three withcolor 0.85white withstacking -1;
  draw back_three dashed evenly withstacking -1;

  label.ulft("\tfx $+$", sum_one.w + (2bp,2bp)); 
  label.lrt("\tfx $-$",  sum_one.s + (2bp,0) ); 

  label.ulft("\tfx $+$", sum_two.w + (2bp,2bp)) withstacking 1; 
  label.lrt("\tfx $-$",  sum_two.s + (2bp,0) )  withstacking 1; 

  label.ulft("\tfx $+$", sum_three.w + (2bp,2bp)) withstacking 1; 
  label.lrt("\tfx $-$",  sum_three.s + (2bp,0) )  withstacking 1; 

  label.lft("$U(s)$", Input);
  label.rt("$Y(s)$", Output);

  currentpicture := currentpicture scaled 1.5;

  endgroup;
\stopMPpage % }}}
\stopstackingsteps

\startstackingsteps[{0,1,12,14},{-2,0,12,14,2}, {-3,0,14,3,30}, {-4,0,30,4,14,40},{0,30,5}]
\startMPpage[offset=1dk, stacking={\namedstackingparameter\empty{criterium}}] % {{{ Example 3 begingroup;
  sketchypaths;

  ux := 1cm;
  uy := 1cm;

  pair split_one, split_two;
  boxit.G_one("$G_1(s)$");
  boxit.G_two("$G_2(s)$");
  boxit.G_three("$G_3(s)$");
  boxit.G_four("$G_4(s)$");
  boxit.H_one("$H_1(s)$");
  boxit.H_two("$H_2(s)$");
  boxit.H_three("$H_3(s)$");
  circleit.sum_one("\tfc $+$");
  circleit.sum_two("\tfc $+$");
  circleit.sum_three("\tfc $+$");

  split_one = origin;

  G_one.w - sum_one.e = (1.5ux,0);
  sum_two.w - G_one.e = (1.5ux,0);
  G_two.w - sum_two.e = (1.5ux,0);
  sum_three.w - G_two.e = (1.5ux,0);
  
  G_three.w - sum_three.e = (1.5ux,0);

  split_one - G_three.e = (1ux,0);
  G_four.w - split_one  = (1ux,0);

  split_two - G_four.e = (1ux, 0); 

  0.5[G_three.s, G_four.s]  - H_one.n = (0, uy);
  xpart H_three.n = xpart G_two.s ;
  ypart H_one.s - ypart H_three.n = uy;

  H_two.s - G_two.n = (0,uy);

  boxes_fixsize(sum_one, sum_two, sum_three, G_one, G_two, G_three, G_four, H_one, H_two, H_three);
  boxes_fixpos(sum_one, sum_two, sum_three, G_one, G_two, G_three, G_four, H_one, H_two, H_three);

  circleit.nsum_one("\tfc $+$");
  boxit.nG_one("$G_1(s)$");

  circleit.nsum_two("\tfc $+$");
  boxit.nG_two("$G_2(s)$");

  nsum_one.c = G_one.c;
  nG_one.c = sum_one.c;

  nsum_two.c = G_two.c;
  nG_two.c = sum_three.c;

  boxit.new_one("$\dfrac{1}{G_4(s)}$");
  xpart new_one.c = xpart G_four.c;
  ypart new_one.c = ypart H_two.c;

  boxit.new_two("$G_1(s)$");
  xpart new_two.c = xpart(0.5[nsum_one.c, sum_two.c]);
  ypart new_two.c = ypart H_three.c;

  boxit.new_three("$\dfrac{1}{G_2(s)}$");
  xpart new_three.c = xpart sum_three.c;
  ypart new_three.c = ypart H_one.c;


  boxes_fixsize(new_one, new_two, new_three, nsum_one, nsum_two, nG_one, nG_two);
  boxes_fixpos (new_one, new_two, new_three, nsum_one, nsum_two, nG_one, nG_two);

  draw boxes_pic sum_one shifted sum_one.c withstacking 14;
  draw boxes_pic sum_two shifted sum_two.c;
  draw boxes_pic sum_three shifted sum_three.c withstacking 14;
  draw boxes_pic G_one shifted G_one.c withstacking 14;
  draw boxes_pic G_two shifted G_two.c withstacking 14;
  draw boxes_pic G_three shifted G_three.c;
  draw boxes_pic G_four shifted G_four.c;

  draw boxes_pic new_one shifted new_one.c withstacking 30;
  draw boxes_pic new_two shifted new_two.c withstacking 5;
  draw boxes_pic new_three shifted new_three.c withstacking 5;

  draw boxes_pic nsum_one shifted nsum_one.c withstacking 5;
  draw boxes_pic nG_one shifted nG_one.c withstacking 5;
  draw boxes_pic nsum_two shifted nsum_two.c withstacking 5;
  draw boxes_pic nG_two shifted nG_two.c withstacking 5;

  draw boxes_pic H_one shifted H_one.c;
  draw boxes_pic H_two shifted H_two.c;
  draw boxes_pic H_three shifted H_three.c;

  pair Input, Output;

  Input  := sum_one.w - (1.5ux,0);
  Output := split_two + (ux,0);

  newpath back_one, back_two, back_three;

  back_one = bbox image( 
      drawboxed(G_four);
      draw fullcircle shifted (xpart split_one, ypart H_two.c);
      draw fullcircle shifted split_two;
    ) enlarged 5pt;

  fill back_one withcolor 0.85white withstacking -2;
  draw back_one dashed evenly withstacking -2;

  back_two = bbox image( 
      drawboxed(sum_one, G_one);
      draw fullcircle shifted (xpart sum_one.c, ypart H_one.c);
    ) enlarged 5pt;

  fill back_two withcolor 0.85white withstacking -4;
  draw back_two dashed evenly withstacking -4;

  back_three = bbox image( 
      drawboxed(sum_three, G_two);
      draw fullcircle shifted (xpart sum_three.c, ypart H_one.c);
    ) enlarged 5pt;

  fill back_three withcolor 0.85white withstacking -4;
  draw back_three dashed evenly withstacking -4;


  for i = 1 upto 5 :
    sketch_amount := 1.3bp;
    draw bpath G_one cornered 10bp withstacking 14;
    draw bpath G_two cornered 10bp withstacking 14;
    draw bpath G_three cornered 10bp;
    draw bpath G_four cornered 10bp;
    draw bpath H_one cornered 10bp;
    draw bpath H_two cornered 10bp;
    draw bpath H_three cornered 10bp;
    draw bpath sum_one withstacking 14;
    draw bpath sum_two;
    draw bpath sum_three withstacking 14;

    draw bpath new_one cornered 10bp withstacking 30;
    draw bpath new_two cornered 10bp withstacking 5;
    draw bpath new_three cornered 10bp withstacking 5;
    draw bpath nG_one cornered 10bp withstacking 5;
    draw bpath nG_two cornered 10bp withstacking 5;
    draw bpath nsum_one withstacking 5;
    draw bpath nsum_two withstacking 5;

    sketch_amount := 0.6bp;
    drawarrow Input -- lft sum_one.w withstacking 14;
    drawarrow Input -- lft nG_one.w withstacking 5;

    drawarrow sum_one.e -- lft G_one.w withstacking 14;
    drawarrow nG_one.e -- lft nsum_one.w withstacking 5;

    drawarrow G_one.e -- lft sum_two.w withstacking 14;
    drawarrow nsum_one.e -- lft sum_two.w withstacking 5;

    drawarrow sum_two.e -- lft G_two.w withstacking 14;
    drawarrow G_two.e -- lft sum_three.w withstacking 14;
    drawarrow sum_three.e -- lft G_three.w withstacking 14;

    drawarrow sum_two.e -- lft nsum_two.w withstacking 5;
    drawarrow nsum_two.e -- lft nG_two.w withstacking 5;
    drawarrow nG_two.e -- lft G_three.w withstacking 5;

    drawarrow G_three.e -- lft G_four.w;
    drawarrow G_four.e -- Output;

    drawarrow split_one +- rt H_two.e withstacking 12;
    drawarrow H_two.w -+ top sum_two.n;

    drawarrow split_two +- lft H_one.e;
    drawarrow H_one.w -+ bot sum_three.s withstacking 14;
    drawarrow H_one.w -- rt new_three.e withstacking 5;
    drawarrow new_three.w -+ bot nsum_two.s withstacking 5;

    drawarrow split_two +- lft H_three.e;
    drawarrow H_three.w -+ bot sum_one.s withstacking 14;
    drawarrow H_three.w -- lft new_two.e withstacking 5;
    drawarrow new_two.w -+ bot nsum_one.s withstacking 5;

    drawarrow split_two +- rt new_one.e withstacking 30;
    drawarrow new_one.w -- rt H_two.e withstacking 30;

  endfor;

  fill fullcircle scaled 3bp shifted split_one withcolor 0.85red 
       withstacking 2;

  label.ulft("\tfx $+$", sum_one.w + (2bp,2bp)) withstacking 14; 
  label.lrt("\tfx $-$",  sum_one.s + (2bp,0) ) withstacking 14; 

  label.ulft("\tfx $+$", nsum_one.w + (2bp,2bp)) withstacking 5; 
  label.lrt("\tfx $-$",  nsum_one.s + (2bp,0) ) withstacking 5; 

  label.ulft("\tfx $+$", sum_two.w + (2bp,2bp)); 
  label.urt("\tfx $-$",  sum_two.n + (2bp,0) ) ; 

  label.ulft("\tfx $+$", nsum_two.w + (2bp,2bp)) withstacking 5; 
  label.lrt("\tfx $-$",  nsum_two.s + (2bp,0) )  withstacking 5; 

  label.ulft("\tfx $+$", sum_three.w + (2bp,2bp)) withstacking 14; 
  label.lrt("\tfx $-$",  sum_three.s + (2bp,0) ) withstacking 14; 

  label.lft("$U(s)$", Input);
  label.rt("$Y(s)$", Output);

  currentpicture := currentpicture scaled 1.5;

  endgroup;
\stopMPpage % }}}
\stopstackingsteps

\stoptext
