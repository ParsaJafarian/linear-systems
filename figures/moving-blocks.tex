\usepath[/home/adityam/Projects/presentations]

\enablemode[euler]
\environment env-fonts.tex

\startMPdefinitions
% Arrowhead Modifications for TAOCP. Copied from some webpage of Knuth. 
% These arrows are better in appearance than the default mp arrows.
ahangle := 65;
ahlength := 10 ;
vardef arrowhead expr p =
  save q, e, f, g; path q; pair e; pair f; pair g;
  e = point length p of p;
  q = gobble(p shifted -e cutafter makepath(pencircle scaled 2ahlength))
    cuttings;
  f = point 0 of (q rotated 0.5ahangle) shifted e;
  g = point length q of (reverse q rotated -0.5ahangle) shifted e;
  f .. {dir (angle direction length q of (q rotated 0.5ahangle) - 0.3ahangle)}e
    & e{dir (angle direction 0 of ((reverse q) rotated -0.5ahangle)+0.3ahangle)} .. g
enddef;

vardef arrowpath expr p =
  p
enddef;

def mfun_draw_arrow_path text t =
    if autoarrows :
        set_ahlength(t) ;
    fi
    draw arrowpath mfun_arrow_path t ;
    draw arrowhead mfun_arrow_path t ;
    endgroup ;
enddef ;

def mfun_draw_arrow_path_double text t =
    if autoarrows :
        set_ahlength(t) ;
    fi
    draw arrowpath (reverse arrowpath mfun_arrow_path) t ;
    draw arrowhead mfun_arrow_path t ;
    draw arrowhead reverse mfun_arrow_path t ;
    endgroup ;
enddef ;


  primarydef a -+ b =
    a -- (xpart b, ypart a) -- b
  enddef ;

  primarydef a +- b =
    a -- (xpart a, ypart b) -- b
  enddef ;

  input boxes;
  input mp-sketch;
  sketchypaths;
  sketch_amount := 1.3bp;
  pickup pencircle scaled 2bp;
  defaultdx := 10bp;
  defaultdy := 10bp;

\stopMPdefinitions

\starttext
\startMPpage[offset=1dk]
  ux := 1cm;
  uy := 1cm;

  picture one, two;

  one = image(
      boxit.block_one("$G(s)$");

      pair Input, Output_one, Output_two;

      block_one.c = origin;
      drawunboxed(block_one);

      pair split_one;

      Input = block_one.w - (1.5ux,0);
      split_one = block_one.e + (ux,0);
      Output_one = split_one + (1.5ux,0);
      Output_two = split_one + (1.5ux,-uy);

      for i = 1 upto 5 :
        sketch_amount := 1.3bp;
        draw bpath block_one cornered 10bp;

        sketch_amount := 0.6bp;
        drawarrow Input -- lft block_one.w;
        drawarrow block_one.e -- Output_one;
        drawarrow split_one +- Output_two;

      endfor

      fill fullcircle scaled 3bp shifted split_one withcolor 0.85red;

      label.lft("$X(s)$", Input);
      label.rt ("$Y(s)$", Output_one);
      label.rt ("$Z(s)$", Output_two);
  );

  two = image(
      %boxit.block_one("$G(s)$");
      boxit.block_two("$G(s)$");

      newpair Input, Output_one, Output_two;

      % block_one.c = origin;
      block_one.s - block_two.n = (0,uy);
      drawunboxed(block_one,block_two);

      newpair split_one;

      split_one = block_one.w - (1.5ux,0);
      Input = split_one - (1ux,0);
      Output_one = block_one.e + (1ux,0);
      Output_two = block_two.e + (1ux,0);

      for i = 1 upto 5 :
        sketch_amount := 1.3bp;
        draw bpath block_one cornered 10bp;
        draw bpath block_two cornered 10bp withcolor 0.85red;

        sketch_amount := 0.6bp;
        drawarrow Input -- lft block_one.w;
        drawarrow block_one.e -- Output_one;
        drawarrow split_one +- lft block_two.w;
        drawarrow block_two.e -- Output_two;
      endfor

      fill fullcircle scaled 3bp shifted split_one withcolor 0.85red;


      label.lft("$X(s)$", Input);
      label.rt ("$Y(s)$", Output_one);
      label.rt ("$Z(s)$", Output_two);
  );


  boxit.part_one(one);
  boxit.part_two(two);
  boxit.equiv("\tfc $≡$");

  equiv.c = origin;
  equiv.w - part_one.e = (ux,0);
  part_two.w - equiv.e = (ux,0);
  
  drawunboxed(part_one, equiv, part_two);

  currentpicture := currentpicture scaled 1.5;
\stopMPpage


\startMPpage[offset=1dk]
  ux := 1cm;
  uy := 1cm;

  sketchypaths;

  newpicture one, two;

  one = image(
      boxit.block_one("$G(s)$");

      pair Input, Output_one, Output_two;

      block_one.c = origin;
      drawunboxed(block_one);

      pair split_one;

      split_one = block_one.w - (ux,0);
      Input = split_one - (ux,0);
      Output_one = block_one.e + (1.5ux,0);
      Output_two = (xpart Output_one, ypart block_one.s - uy);

      for i = 1 upto 5 :
        sketch_amount := 1.3bp;
        draw bpath block_one cornered 10bp;

        sketch_amount := 0.6bp;
        drawarrow Input -- lft block_one.w;
        drawarrow block_one.e -- Output_one;
        drawarrow split_one +- Output_two;

      endfor

      fill fullcircle scaled 3bp shifted split_one withcolor 0.85red;

      label.lft("$X(s)$", Input);
      label.rt ("$Y(s)$", Output_one);
      label.rt ("$Z(s)$", Output_two);
  );

  two = image(
      %boxit.block_one("$G(s)$");
      boxit.block_two("$\dfrac{1}{G(s)}$");

      newpair Input, Output_one, Output_two;
      newpair split_one;

      Input = block_one.w - (1.5ux,0);
      ypart block_one.s - ypart block_two.n = uy;
      split_one = block_one.e + (ux,0);

      xpart block_two.w - xpart split_one = ux;
      Output_two = block_two.e + (ux,0);
      Output_one = (xpart Output_two, ypart block_one.e);

      drawunboxed(block_one,block_two);

      for i = 1 upto 5 :
        sketch_amount := 1.3bp;
        draw bpath block_one cornered 10bp;
        draw bpath block_two cornered 10bp withcolor 0.85red;

        sketch_amount := 0.6bp;
        drawarrow Input -- lft block_one.w;
        drawarrow block_one.e -- Output_one;
        drawarrow split_one +- lft block_two.w;
        drawarrow block_two.e -- Output_two;
      endfor

      fill fullcircle scaled 3bp shifted split_one withcolor 0.85red;


      label.lft("$X(s)$", Input);
      label.rt ("$Y(s)$", Output_one);
      label.rt ("$Z(s)$", Output_two);
  );


  boxit.part_one(one);
  boxit.part_two(two);
  boxit.equiv("\tfc $≡$");

  equiv.c = origin;
  equiv.w - part_one.e = (ux,0);
  part_two.w - equiv.e = (ux,0);
  
  drawunboxed(part_one, equiv, part_two);



  currentpicture := currentpicture scaled 1.5;
\stopMPpage

\startMPpage[offset=1dk]
  ux := 1cm;
  uy := 1cm;

  sketchypaths;

  picture one, two;

  one = image(
      boxit.block_one("$G(s)$");
      circleit.sum_one("\tfc $+$");

      pair Input_one, Input_two, Output;

      block_one.c = origin;
      sum_one.w - block_one.e = (1.5ux,0);
      drawunboxed(block_one,sum_one);

      Input_one = block_one.w - (1.5ux,0);
      Output = sum_one.e + (1.5ux,0);
      Input_two = (xpart Output, ypart sum_one.s - 1.5uy);

      for i = 1 upto 5 :
        sketch_amount := 1.3bp;
        draw bpath block_one cornered 10bp;
        draw bpath sum_one withcolor 0.85red;

        sketch_amount := 0.6bp;
        drawarrow Input_one -- lft block_one.w;
        drawarrow block_one.e -- sum_one.w;
        drawarrow sum_one.e -- Output;
        drawarrow Input_two -+ bot sum_one.s;

      endfor

      label.ulft("\tfx $+$", sum_one.w + (2bp,2bp)); 
      label.lrt("\tfx $+$",  sum_one.s + (2bp,0) ); 

      label.lft("$X(s)$", Input_one);
      label.rt ("$Y(s)$", Input_two);
      label.rt ("$Z(s)$", Output);
  );

  two = image(
      boxit.block_two("$G(s)$");
      boxit.block_three("$\dfrac{1}{G(s)}$");
      circleit.sum_two("\tfc $+$");

      newpair Input_one, Input_two, Output;

      block_two.c = origin;
      block_two.w - sum_two.e = (1.5ux,0);
      block_two.s - block_three.n = (0, uy);
      drawunboxed(block_two,sum_two,block_three);

      Input_one = sum_two.w - (1.5ux,0);
      Output = block_two.e + (1.5ux,0);
      Input_two = block_three.e + (1.5ux,0);

      for i = 1 upto 5 :
        sketch_amount := 1.3bp;
        draw bpath block_two cornered 10bp;
        draw bpath block_three cornered 10bp withcolor 0.85red;
        draw bpath sum_two withcolor 0.85red;

        sketch_amount := 0.6bp;
        drawarrow Input_one -- lft sum_two.w;
        drawarrow sum_two.e -- lft block_two.w;
        drawarrow block_two.e -- Output;
        drawarrow Input_two -- rt block_three.e;
        drawarrow block_three.w -+ sum_two.s;

      endfor

      label.ulft("\tfx $+$", sum_two.w + (2bp,2bp)); 
      label.lrt("\tfx $+$",  sum_two.s + (2bp,0) ); 

      label.lft("$X(s)$", Input_one);
      label.rt ("$Y(s)$", Input_two);
      label.rt ("$Z(s)$", Output);
  );


  boxit.part_one(one);
  boxit.part_two(two);
  boxit.equiv("\tfc $≡$");

  equiv.c = origin;
  equiv.w - part_one.e = (ux,0);
  part_two.w - equiv.e = (ux,0);
  
  drawunboxed(part_one, equiv, part_two);

  currentpicture := currentpicture scaled 1.5;
\stopMPpage

\startMPpage[offset=1dk]
  ux := 1cm;
  uy := 1cm;

  sketchypaths;

  picture one, two;

  one = image(
      boxit.block_one("$G(s)$");
      circleit.sum_one("\tfc $+$");

      pair Input_one, Input_two, Output;

      block_one.c = origin;
      block_one.w - sum_one.e = (1.5ux,0);
      drawunboxed(block_one,sum_one);

      Input_one = sum_one.w - (1.5ux,0);
      Output = block_one.e + (1.5ux,0);
      Input_two = (xpart Input_one, ypart sum_one.s - 1.5uy);

      for i = 1 upto 5 :
        sketch_amount := 1.3bp;
        draw bpath block_one cornered 10bp;
        draw bpath sum_one withcolor 0.85red;

        sketch_amount := 0.6bp;
        drawarrow Input_one -- lft sum_one.w;
        drawarrow sum_one.e -- lft block_one.w;
        drawarrow block_one.e -- Output;
        drawarrow Input_two -+ bot sum_one.s;

      endfor

      label.ulft("\tfx $+$", sum_one.w + (2bp,2bp)); 
      label.lrt("\tfx $+$",  sum_one.s + (2bp,0) ); 

      label.lft("$X(s)$", Input_one);
      label.lft("$Y(s)$", Input_two);
      label.rt ("$Z(s)$", Output);
  );

  two = image(
      boxit.block_two("$G(s)$");
      boxit.block_three("$G(s)$");
      circleit.sum_two("\tfc $+$");

      newpair Input_one, Input_two, Output;

      block_two.c = origin;
      sum_two.w - block_two.e = (1.5ux,0);
      block_two.s - block_three.n = (0, uy);
      drawunboxed(block_two,sum_two,block_three);

      Input_one = block_two.w - (1.5ux,0);
      Output = sum_two.e + (1.5ux,0);
      Input_two = block_three.w - (1.5ux,0);

      for i = 1 upto 5 :
        sketch_amount := 1.3bp;
        draw bpath block_two cornered 10bp;
        draw bpath block_three cornered 10bp withcolor 0.85red;
        draw bpath sum_two withcolor 0.85red;

        sketch_amount := 0.6bp;
        drawarrow Input_one -- lft block_two.w;
        drawarrow block_two.e -- lft sum_two.w;
        drawarrow sum_two.e -- Output;
        drawarrow Input_two -- rt block_three.w;
        drawarrow block_three.e -+ sum_two.s;

      endfor

      label.ulft("\tfx $+$", sum_two.w + (2bp,2bp)); 
      label.lrt("\tfx $+$",  sum_two.s + (2bp,0) ); 

      label.lft("$X(s)$", Input_one);
      label.lft("$Y(s)$", Input_two);
      label.rt ("$Z(s)$", Output);
  );


  boxit.part_one(one);
  boxit.part_two(two);
  boxit.equiv("\tfc $≡$");

  equiv.c = origin;
  equiv.w - part_one.e = (ux,0);
  part_two.w - equiv.e = (ux,0);
  
  drawunboxed(part_one, equiv, part_two);

  currentpicture := currentpicture scaled 1.5;
\stopMPpage

\stoptext
